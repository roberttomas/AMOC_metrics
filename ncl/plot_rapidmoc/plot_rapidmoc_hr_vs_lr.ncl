begin
;---------------------------------------------------------------------------------------------------------------------------------------
; input files generated by RapidMoc
; lr_2000-2010_natl_meridional_transports_at_26N.nc             low-res pop, reagraged to put GM near the middle of the grid
; hr_2009-2010_natl_meridional_transports_at_26N.nc             high-res pop, reagraged to put GM near the middle of the grid
; hr_ss_2007-2017_natl_meridional_transports_at_26N.nc          high-res pop, reagraged to put GM near the middle of the grid and sub-sampled every 10th point
;---------------------------------------------------------------------------------------------------------------------------------------
   lkludge_mto0 = True  ; subtract bottom level value from profile showing overturning calculated using model velocities so that it is forced to zero
   lplot_asd = False
;
; RapidMoc data file - hr model
   file_in = "/glade/work/tomas/RapidMoc/data/g.e20.G.TL319_t13.control.001_hfreq/hr_2000-2010_natl_meridional_transports_at_26N.nc"
;
   f_in = addfile(file_in, "r")
   sf_rapid_hr = dim_avg_n_Wrap(f_in->sf_rapid, 0)
;   printVarSummary(sf_rapid_hr)
   sf_model_hr = dim_avg_n_Wrap(f_in->sf_model, 0)
;   printVarSummary(sf_model_hr)
   depth_hr = f_in->depth
;
; components
   sf_ek_hr     = dim_avg_n_Wrap(f_in->sf_ek, 0);        "Ekman overturning streamfunction."
   sf_geoint_hr = dim_avg_n_Wrap(f_in->sf_geoint, 0);    "Geostrophic interior overturning streamfunction."
   sf_wbw_hr    = dim_avg_n_Wrap(f_in->sf_wbw, 0);       "Western boundary wedge overturning streamfunction."
   sf_fc_hr     = dim_avg_n_Wrap(f_in->sf_fc, 0);        "Florida current overturning streamfunction." ;
   sf_mo_hr     = dim_avg_n_Wrap(f_in->sf_mo, 0);        "Mid ocean overturning streamfunction (sf_mo = sf_wbw + sf_int)." 
;---------------------------------------------------------------------------------------------------------------------------------------
; ASD data file - hr model
   file_in = "/glade/work/tomas/RapidMoc/data/hybrid_v5_rel04_BC5_ne120_t12_pop62/hr_2034-2044_natl_meridional_transports_at_26N.nc"

   f_in = addfile(file_in, "r")
   sf_rapid_asd = dim_avg_n_Wrap(f_in->sf_rapid, 0)
;   printVarSummary(sf_rapid_asd)
   sf_model_asd = dim_avg_n_Wrap(f_in->sf_model, 0)
;   printVarSummary(sf_model_asd)
   depth_asd = f_in->depth
;
; components
   sf_ek_asd     = dim_avg_n_Wrap(f_in->sf_ek, 0);        "Ekman overturning streamfunction."
   sf_geoint_asd = dim_avg_n_Wrap(f_in->sf_geoint, 0);    "Geostrophic interior overturning streamfunction."
   sf_wbw_asd    = dim_avg_n_Wrap(f_in->sf_wbw, 0);       "Western boundary wedge overturning streamfunction."
   sf_fc_asd     = dim_avg_n_Wrap(f_in->sf_fc, 0);        "Florida current overturning streamfunction." ;
   sf_mo_asd     = dim_avg_n_Wrap(f_in->sf_mo, 0);        "Mid ocean overturning streamfunction (sf_mo = sf_wbw + sf_int)." 
;---------------------------------------------------------------------------------------------------------------------------------------
; RapidMoc data file - lr model
   file_in = "/glade/work/tomas/RapidMoc/data/g20a10.GIAF_JRA.gx1v7.C03/lr_2000-2010_natl_meridional_transports_at_26N.nc"
; 
   f_in = addfile(file_in, "r")
   sf_rapid_lr = dim_avg_n_Wrap(f_in->sf_rapid, 0)
;   printVarSummary(sf_rapid_lr)
   sf_model_lr = dim_avg_n_Wrap(f_in->sf_model, 0)
;   printVarSummary(sf_model_lr)
   depth_lr = f_in->depth
;
; components
   sf_ek_lr     = dim_avg_n_Wrap(f_in->sf_ek, 0);        "Ekman overturning streamfunction."
   sf_geoint_lr = dim_avg_n_Wrap(f_in->sf_geoint, 0);    "Geostrophic interior overturning streamfunction."
   sf_wbw_lr    = dim_avg_n_Wrap(f_in->sf_wbw, 0);       "Western boundary wedge overturning streamfunction."
   sf_fc_lr     = dim_avg_n_Wrap(f_in->sf_fc, 0);        "Florida current overturning streamfunction." ;
   sf_mo_lr     = dim_avg_n_Wrap(f_in->sf_mo, 0);        "Mid ocean overturning streamfunction (sf_mo = sf_wbw + sf_int)." 
;---------------
;   do nk = 0, dimsizes(depth_lr) - 1
;      print("lr: " + depth_lr(nk) + "  hr: " + depth_hr(nk) )
;   end do
;   exit
;---------------
; read in pop MOC variable
;--------
; HR g-run
   file_in = "/gpfs/fs1/p/cgd/oce/projects/JRA55/IAF/g.e20.G.TL319_t13.control.001/ocn/proc/g.e20.G.TL319_t13.control.001.pop.h.000101-006112.MOC.nc"
   f_in = addfile(file_in, "r")

   nt_min = 42*12
   nt_max = 53*12 - 1
;  float MOC(time, transport_reg, moc_z, lat_aux_grid) ; transport_reg: 0-global; 1-Atlantic
   moc_temp = dim_avg_n_Wrap(f_in->MOC(nt_min:nt_max,1,:,{26.5}),0)
   moc_z_hr = f_in->moc_z
   moc_z_hr = (/ moc_z_hr/100. /) ; cm to m
   moc_z_hr@units = "meters"
   moc_z_hr@valid_max = 5499.91
   moc_temp&moc_z = moc_z_hr
   moc_pop_hr = linint1(moc_z_hr, moc_temp, False, depth_hr, 0)
   moc_pop_hr!0 = "depth"
   moc_pop_hr&depth = depth_hr
   delete(moc_temp)
;   printVarSummary(moc_pop_hr)
;--------
; LR
   file_in = "/glade/work/tomas/RapidMoc/data/g20a10.GIAF_JRA.gx1v7.C03/g20a10.GIAF_JRA.gx1v7.C03.pop.h.MOC.004301-005212.nc"
   f_in = addfile(file_in, "r")
;-----------
;   float MOC(time, transport_reg, moc_comp, moc_z, lat_aux_grid) ;
;
;   transport_regions =
;  "Global Ocean - Marginal Seas",
;  "Atlantic Ocean + Mediterranean Sea + Labrador Sea + GIN Sea + Arctic Ocean + Hudson Bay" ;
;
;   moc_components =
;  "Eulerian Mean",
;  "Eddy-Induced (bolus)",
;  "Submeso" ;

; Atlantic Ocean(2nd subscript, 1) +, Eulerian Mean(3rd subscript, 0)
   moc_temp = dim_avg_n_Wrap(f_in->MOC(:,1,0,:,{26.5}),0)
   moc_z_lr        = f_in->moc_z
   moc_z_lr = (/ moc_z_lr/100. /) ; cm to m
   moc_z_lr@units = "meters"
   moc_z_lr@valid_max = 5499.91
   moc_temp&moc_z = moc_z_lr
   moc_pop_lr = linint1(moc_z_lr, moc_temp, False, depth_lr, 0)
   moc_pop_lr!0 = "depth"
   moc_pop_lr&depth = depth_lr
   delete(moc_temp)
;   printVarSummary(moc_pop_lr)
;---------------
; read in Rapid MOC observations
;--------
   file_in = "/glade/work/tomas/RapidMoc/data/observations/moc_vertical.nc"
   f_in    = addfile(file_in, "r")
   temp = f_in->stream_function_mar
   depth_rapid               = f_in->depth

; calculate monthly values from 2x daily, Apr 1, 2004 to Feb 28, 2017
; I couldn't get calculate_monthly_values to work unless time was the 0 dimenstion
   temp_2xday_depth = temp(time|:, depth|:)
   temp_mon = calculate_monthly_values(temp_2xday_depth, "avg", 0, False)
   time_rapid = temp_mon&time
   ttt = cd_calendar(time_rapid, 3)
   ind_obs_end = 69
   sf_rapid_temp = dim_avg_n_Wrap(temp_mon(0:ind_obs_end,:),0) ; April 2004 (0) to December 2009 (69)
   print("Using Rapid Observations from: " + ttt(0) + " to " + ttt(ind_obs_end))
   
   delete(temp)
   delete(temp_2xday_depth)
   delete(temp_mon)
   sf_rapid = linint1(depth_rapid, sf_rapid_temp, False, depth_hr, 0)
   printVarSummary(sf_rapid)
   delete(sf_rapid_temp)
;======================================================================================================================
   print("Plotting")
   script_name = get_script_prefix_name()
   time_date = systemfunc("date")
   file_out = script_name + ".png"
   wks_type = "png"
   wks_type@wkWidth = 1024   ; 1024 is default
   wks_type@wkHeight = 1024  ; 1024 is default
   wks = gsn_open_wks(wks_type,file_out)
;
   res=True
   res@gsnDraw = False
   res@gsnFrame = False
;
   res@trYReverse = True
   res@vpWidthF   = 0.6
   res@vpHeightF  = 0.6
;
   res@tiXAxisString = "Moc(Sv)"
   res@tiYAxisString = "Depth(m)"
;
; add a legend
   res@pmLegendDisplayMode    = "Always"              ; turn on legend
; 
   res@pmLegendSide           = "Bottom"              ; Change location of 
   res@pmLegendParallelPosF   =  0.65                 ; move units right
   res@pmLegendOrthogonalPosF = -0.60                 ; more neg = down
; 
   res@pmLegendWidthF         = 0.25                  ; Change width and
   res@pmLegendHeightF        = 0.25                  ; height of legend.
   res@lgLabelFontHeightF     = .017                  ; change font height
   res@lgPerimOn              = False                 ; no box around
;
   plot  = new(6, graphic)
   plot_data = new( (/ 5, dimsizes(depth_hr) /), typeof(depth_hr) )
;   printVarSummary(plot_data)
;
   res@xyLineThicknesses = (/ 3.0, 3.0, 3.0, 3.0, 3.0 /)
   res@xyDashPatterns = (/ 2, 12, 2, 12, 1 /)
   res@xyLineColors   = (/ "red", "red", "blue", "blue", "red" /)
;
   res@tiMainString  = "Atlantic Overturning at 26.5N"
   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_rapid_lr
   plot_data(1,0:dimsizes(depth_lr) - 1) = sf_model_lr
   plot_data(2,:) = sf_rapid_hr
   plot_data(3,:) = sf_model_hr
;
   plot_data(4,0:dimsizes(depth_lr) - 1) = moc_pop_lr
   res@xyExplicitLegendLabels = (/"LR - rapid","LR - model","HR - rapid","HR - model", "LR - POP EM"/)
   plot(0) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
;
   plot_data := new( (/ 2, dimsizes(depth_hr) /), typeof(depth_hr) )
   res@tiMainString  = "Florida Current component at 26.5N"
   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_fc_lr
   plot_data(1,:) = sf_fc_hr
   res@xyLineThicknesses := (/ 3.0, 3.0 /)
   res@xyDashPatterns := (/ 0, 0 /)
   res@xyLineColors   := (/ "red", "blue" /)
   res@xyExplicitLegendLabels := (/"LR","HR" /)
   plot(1) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
;
   plot_data := new( (/ 2, dimsizes(depth_hr) /), typeof(depth_hr) )
   res@tiMainString  = "WBW component at 26.5N"
   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_wbw_lr
   plot_data(1,:) = sf_wbw_hr
   res@xyLineThicknesses := (/ 3.0, 3.0 /)
   res@xyDashPatterns := (/ 0, 0 /)
   res@xyLineColors   := (/ "red", "blue" /)
   res@xyExplicitLegendLabels := (/"LR","HR" /)
   plot(2) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
;
   plot_data := new( (/ 2, dimsizes(depth_hr) /), typeof(depth_hr) )
   res@tiMainString  = "Geo-int component at 26.5N"
   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_geoint_lr
   plot_data(1,:) = sf_geoint_hr

   res@xyLineThicknesses := (/ 3.0, 3.0 /)
   res@xyDashPatterns := (/ 0, 0 /)
   res@xyLineColors   := (/ "red", "blue" /)
   res@xyExplicitLegendLabels := (/"LR","HR" /)
   plot(3) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
;
;
   plot_data := new( (/ 2, dimsizes(depth_hr) /), typeof(depth_hr) )

;   res@tiMainString  = "Mid-Ocean (WBW+INT) component at 26.5N"
;   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_mo_lr
;   plot_data(1,:) = sf_mo_hr

; for add all terms
;   res@trXMinF =  -8.
;   res@trXMaxF =  16.
   res@tiMainString  = "FL current + WBW at 26.5N"
   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_fc_lr + sf_wbw_lr ; ;  add all terms  + sf_geoint_lr + sf_ek_lr
   plot_data(1,:) = sf_fc_hr + sf_wbw_hr                        ;  add all terms  + sf_geoint_hr + sf_ek_lr

   res@xyLineThicknesses := (/ 3.0, 3.0 /)
   res@xyDashPatterns := (/ 0, 0 /)
   res@xyLineColors   := (/ "red", "blue" /)
   res@xyExplicitLegendLabels := (/"LR","HR" /)
   plot(4) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
;
   res@tiMainString  = "Ekmann component at 26.5N"
   plot_data := new( (/ 2, dimsizes(depth_hr) /), typeof(depth_lr) )
   plot_data(0,0:dimsizes(depth_lr) - 1) = sf_ek_lr
   plot_data(1,:) = sf_ek_hr
   res@xyLineThicknesses := (/ 3.0, 3.0 /)
   res@xyDashPatterns := (/ 0, 0 /)
   res@xyLineColors   := (/ "red", "blue" /)
   res@xyExplicitLegendLabels := (/"LR","HR" /)
   plot(5) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
;
;
   pres = True
   gsn_panel(wks,plot,(/3,2/),pres)             ;
;   frame(wks)
;
;====================
   plot  := new(1, graphic)
   if( lplot_asd ) then
      plot_data := new( (/ 10, dimsizes(depth_hr) /), typeof(depth_hr) )
   else
      plot_data := new( (/ 7, dimsizes(depth_hr) /), typeof(depth_hr) )
   end if
;   printVarSummary(plot_data)
;
   res@xyLineThicknesses := (/ 5.0, 5.0, 5.0, 5.0, 5.0, 5.0, 5.0, 5.0, 5.0, 5.0 /)
   res@xyDashPatterns := (/ 0, 2, 12, 0, 2, 12, 0, 2, 12, 0 /)
   res@xyLineColors   := (/ "black", "red", "red", "red", "blue", "blue", "blue", "darkgreen", "darkgreen", "darkgreen" /)
;   res@trYMaxF = 6000.
;   res@trYMinF = 5000.
   res@gsnXRefLine = 0.
;
   if( lplot_asd ) then
      res@tiMainString  = "Atlantic Overturning at 26.5N~C~G-runs, JRA55 IAF Cyc. 1; Yrs. 43-52 + B-ASD"
   else
      res@tiMainString  = "Atlantic Overturning at 26.5N~C~G-runs, JRA55 IAF Cyc. 1; Yrs. 43-52"
   end if
   plot_data(0,:) = (/ sf_rapid /)
   plot_data(1,0:dimsizes(depth_lr) - 1) = sf_rapid_lr
;
   if(lkludge_mto0) then
; subtract the deepest value to force 0 at bottom
      plot_data(2,0:dimsizes(depth_lr) - 1) = sf_model_lr - sf_model_lr(dimsizes(sf_model_lr)-1)
   else
      plot_data(2,0:dimsizes(depth_lr) - 1) = sf_model_lr
   end if
;
   plot_data(3,0:dimsizes(depth_lr) - 1) = moc_pop_lr
   plot_data(4,:) = sf_rapid_hr
;
   if(lkludge_mto0) then
; subtract the deepest value to force 0 at bottom
      plot_data(5,:) = sf_model_hr - sf_model_hr(dimsizes(sf_model_hr)-1)
   else
      plot_data(5,:) = sf_model_hr
   end if
;
   plot_data(6,:) = moc_pop_hr
;
   if( lplot_asd ) then
      plot_data(7,:) = sf_rapid_asd
      plot_data(8,:) = sf_model_asd
   end if
;
;   plot_data(9,:) = moc_pop_asd
   if( lplot_asd ) then
      res@xyExplicitLegendLabels := (/"Rapid Obs. 2004-09", "LR - Roberts rapid","LR - Roberts model","LR - POP Eul.Mean", \
                                       "HR - Roberts rapid","HR - Roberts model","HR - POP Eul.Mean", "ASD - Roberts rapid", "ASD - Roberts model", "ASD - POP Eul.Mean" /)
   else
      res@xyExplicitLegendLabels := (/"Rapid Obs. 2004-09", "LR - Roberts rapid","LR - Roberts model","LR - POP Eul.Mean", \
                                       "HR - Roberts rapid","HR - Roberts model","HR - POP Eul.Mean" /)
   end if
   plot(0) = gsn_csm_xy(wks, plot_data, depth_hr, res)         ; 
   gsn_panel(wks,plot,(/1,1/),pres)             ;
end
